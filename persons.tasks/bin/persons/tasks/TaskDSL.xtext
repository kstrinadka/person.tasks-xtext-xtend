grammar persons.tasks.TaskDSL with org.eclipse.xtext.common.Terminals

generate taskDSL "http://www.tasks.persons/TaskDSL"


// Что поменялось?
// добавляем анонимность по галочке (имена не сгенерируются, если флаг анонимности включен)


Planning:
	'Planning' name = ID
	(anonymous ?= 'anonymous')?	// ?= значит что DSL-объекти типа Boolean (включаем логику генерации по галочке)
	(persons += Person |
	tasks += Task)* //если не поставить звездочку, то только первая сущность сможет объявиться
;

Person:
	'Person:' name=ID
;

// для таски тоже можно имя задать, чтоб как для персоны?? (хотя у видов тасок есть различные ID и названия строк)
// Двоеточия теперь почему-то в терминальных символах
Task: 
	'Task:' action = Action
	'persons:' persons += [Person]+
	'priority:' prio = INT
	('duration:' duration = Duration)?
;

Duration:
	dl = INT unit = TimeUnit
;

Action: 
	LunchAction | 
	MeetingAction | 
	PaperAction | 
	PaymentAction;


LunchAction: 
	'Lunch' location = ID;


MeetingAction: 
	'Meeting' topic = STRING;


PaperAction: 
	'Report' report = ID;


PaymentAction: 
	'Pay' amount = INT 'euro';


enum TimeUnit:
	MINUTE = 'min' |
	HOUR = 'hour' |
	DAY = 'day' |
	WEEK = 'week';